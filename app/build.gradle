apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Add secrets from secrets file, thanks to https://medium.com/@geocohn/keeping-your-android-projects-secrets-secret-393b8855765d
def secretsFile = rootProject.file("secrets.properties")
def secrets = new Properties()
secrets.load(new FileInputStream(secretsFile)) // If this fails, you're missing the secrets.properties file in project root, ask someone else for the file (NEVER EVER COMMIT IT)

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "ar.edu.itba.spi.calibracion"
        minSdkVersion 18
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // Add secrets from secrets file to build config
        // buildConfigField("String", "maps_api_key", secrets['maps_api_key'])
        // Add secret as resource string (this was adapted from https://developers.google.com/maps/documentation/android-sdk/map-with-marker)
        resValue "string", "maps_api_key", secrets['maps_api_key']
        resValue "string", "api_private_key", secrets['api_private_key']
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    retrofit_version = '2.5.0'
    rxandroid_version = '2.0.1'
    rxkotlin_version = '2.1.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.google.android.gms:play-services-plus:16.0.0'
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // Lifecycle extensions and ViewModel, etc. See this if we upgrade to sdk version 28: https://stackoverflow.com/a/52455035/2333689
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"

    //ReactiveJava for android
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    //ReactiveJava for Kotlin
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.27.0-eap13'
    //UI Elements
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    //For online requests
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"

    //ReactiveJava adapter for Retrofit
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    //JSON parser adapter for retrofit
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    // Scalars parser (string, etc.)
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"

    //Pretty Logger
    implementation 'com.orhanobut:logger:2.1.1'

    // Google Maps
    implementation 'com.google.android.gms:play-services-maps:16.0.0'

    // JWTs
    api 'io.jsonwebtoken:jjwt-api:0.10.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.10.5'
    runtimeOnly('io.jsonwebtoken:jjwt-orgjson:0.10.5') {
        exclude group: 'org.json', module: 'json' //provided by Android natively
    }
    // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
    //runtimeOnly 'org.bouncycastle:bcprov-jdk15on:1.60'
}
